plugins {
    id "java"
    id "org.springframework.boot" version "2.6.4"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "com.adarshr.test-logger" version "3.2.0"
}

repositories {
    mavenCentral()
}

group = "com.trynoice"
version = System.getenv("BUILD_VERSION") ?: "0.0.0-dev"

sourceCompatibility = "11"

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

springBoot {
    buildInfo()
}

bootRun {
    systemProperty "spring.profiles.active", "dev-default,dev"
}

bootJar {
    archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
}

test {
    useJUnitPlatform()
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    shouldRunAfter test

    classpath = sourceSets.integrationTest.runtimeClasspath
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    useJUnitPlatform()
    systemProperty "spring.profiles.active", "test"
}

check.dependsOn integrationTest

dependencies {
    def springDocVersion = '1.6.6'

    // to enable JavaDoc support in SpringDocs
    annotationProcessor "com.github.therapi:therapi-runtime-javadoc-scribe:0.13.0"
    annotationProcessor "org.projectlombok:lombok"

    compileOnly "org.projectlombok:lombok"

    developmentOnly "org.springframework.boot:spring-boot-devtools"

    implementation "com.auth0:java-jwt:3.18.3"
    implementation "com.github.ben-manes.caffeine:caffeine:3.0.5"
    implementation "com.google.apis:google-api-services-androidpublisher:v3-rev20220221-1.32.1"
    implementation "com.google.auth:google-auth-library-oauth2-http:1.5.3"
    implementation "com.stripe:stripe-java:20.99.0"
    implementation "org.apache.commons:commons-text:1.9"
    implementation "org.flywaydb:flyway-core:8.5.1"
    implementation "org.springframework.boot:spring-boot-starter-cache"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springdoc:springdoc-openapi-javadoc:$springDocVersion"
    // https://github.com/springdoc/springdoc-openapi/issues/198
    implementation "org.springdoc:springdoc-openapi-security:$springDocVersion"
    implementation "org.springdoc:springdoc-openapi-ui:$springDocVersion"
    implementation "org.postgresql:postgresql"

    implementation platform("software.amazon.awssdk:bom:2.17.139")
    implementation "software.amazon.awssdk:s3"
    implementation "software.amazon.awssdk:ses"


    testAnnotationProcessor "org.projectlombok:lombok"

    testCompileOnly "org.projectlombok:lombok"

    testImplementation "org.springframework.boot:spring-boot-starter-test"


    integrationTestAnnotationProcessor "org.projectlombok:lombok"

    integrationTestCompileOnly "org.projectlombok:lombok"

    integrationTestImplementation "org.springframework.boot:spring-boot-starter-test"
    integrationTestImplementation "org.testcontainers:postgresql:1.16.3"
}
